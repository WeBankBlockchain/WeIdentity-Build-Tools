plugins {
    id 'org.springframework.boot' version '2.2.2.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
}

def gradleVer = "4"
if (gradle.gradleVersion.startsWith("6")
        || gradle.gradleVersion.startsWith("5")
        || gradle.gradleVersion.startsWith("4.10")
        || gradle.gradleVersion.startsWith("4.9")
        || gradle.gradleVersion.startsWith("4.8")
        || gradle.gradleVersion.startsWith("4.7")) {
    println "Gradle with version >= 4.7 detected"
    gradleVer = "5"
} else {
    println "Gradle with version < 4.7 detected"
}

apply plugin: 'maven'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'idea'
version = "1.0.12"

// Specify JDK version - may vary in different scenarios
sourceCompatibility = 1.8
targetCompatibility = 1.8

[compileJava, compileTestJava, javadoc]*.options*.encoding = 'UTF-8'
// In this section you declare where to find the dependencies of your project
repositories {
    if (gradle.startParameter.isOffline()) {
        maven {
            url 'dependencies'
        }
    } else {
        mavenLocal()
        mavenCentral()
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public/"
        }
        maven { url "https://dl.bintray.com/ethereum/maven/" }
        maven { url "https://oss.sonatype.org/content/groups/public/" }
    }
}

List lombok = [
        "org.projectlombok:lombok:1.18.12"
]

List jcommander = [
        "com.beust:jcommander:1.72"
]

List log4j = [
        "org.apache.logging.log4j:log4j-api:2.13.3",
        "org.apache.logging.log4j:log4j-web:2.13.3",
        "org.apache.logging.log4j:log4j-core:2.13.3",
        "org.apache.logging.log4j:log4j-jcl:2.13.3",
        "org.apache.logging.log4j:log4j-slf4j-impl:2.13.3",
        "org.slf4j:jul-to-slf4j:1.7.30",
        "org.slf4j:slf4j-api:1.7.30"
]

List json = [
        "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.11.0",
        "com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.11.0",
        "com.fasterxml.jackson.module:jackson-module-parameter-names:2.11.0",
        "com.fasterxml.jackson.core:jackson-databind:2.11.0",
        "com.fasterxml.jackson.core:jackson-annotations:2.11.0",
        "com.fasterxml.jackson.core:jackson-core:2.11.0",
        "com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.11.0"
]

List mysql_driver = [
        "mysql:mysql-connector-java:8.0.20"
]

configurations {
   localDeps
   all*.exclude group: "org.slf4j", module: "slf4j-log4j12"
   all*.exclude group: "ch.qos.logback"
   all*.exclude group: "org.apache.logging.log4j", module: "log4j-to-slf4j"
}

// In this section you declare the dependencies for your production and test code
dependencies {
    localDeps lombok
    compile('org.springframework.boot:spring-boot-starter-web')
    //compile("org.springframework.boot:spring-boot-devtools")
    compile("org.springframework.boot:spring-boot-starter-websocket")
    if (gradleVer.startsWith("4")) {
        if (!gradle.startParameter.isOffline()) {
            compile lombok, jcommander, log4j, json, mysql_driver
            compile("com.webank:weid-java-sdk:${weidSdkVersion}")
        }
    }
    if (gradleVer.startsWith("5")) {
        if (!gradle.startParameter.isOffline()) {
            compileOnly 'org.projectlombok:lombok:1.18.12'
            annotationProcessor 'org.projectlombok:lombok:1.18.12'
            compile lombok, jcommander, log4j, json, mysql_driver
			compile("com.webank:weid-java-sdk:${weidSdkVersion}")
        } else {
            compileOnly files('dist/lib/lombok-1.18.12.jar')
            annotationProcessor files('dist/lib/lombok-1.18.12.jar')
        }
    }
    compile fileTree(dir: 'ext', include: '*.jar')
}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java']
        }
        resources {
            srcDirs = ['src/main/resources']
        }
    }
}

bootRun {
   systemProperty "jdk.tls.namedGroups", "${jdkTlsNamedGroups}"
}

eclipse {
    classpath {
        downloadSources = false
    }
}

jar {
    enabled = true
    destinationDir file('dist/app')
    archiveName project.name + '-' + version + '.jar'
    classifier = 'boot'
    excludes = ["*.jar","*.key","*.ttf","*.crt","*.xml","*.properties"]
    
    doLast {
        copy {
            from file('ext/NotoSansCJKtc-Regular.ttf')
            into 'resources/'
        }
        copy {
            from file('src/main/resources/log4j2.xml')
            into 'resources/'
        }
     	copy {
            from file('resources/')
            into 'dist/conf'
        }
        if (!gradle.startParameter.isOffline()) {
            copy {
                from configurations.runtime
                from configurations.localDeps
                into 'dist/lib'
            }
        }
    }
}
